//Android
ext {
    androidMinSdkVersion = 28
    androidTargetSdkVersion = 33
    androidCompileSdkVersion = 33
}

//Libraries
ext {
    // Kotlin
    ktxVersion = '1.9.0'
    // Compose
    compose_ui_version = '1.3.3'
    // Glide
    glideVersion = '4.10.0'
    // Dagger
    daggerVersion = '2.44.2'
    // Retrofit
    okhttp3Version = '4.9.3'
    retrofitVersion = '2.9.0'
    //Coroutines
    coroutinesVersion = '1.6.4'
    //Development
    leakCanaryVersion = '2.10'
    // Logs + Crash Reporting
    timberVersion = '4.7.0'
}


def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

/******* Modules Dependencies *******/

ext.presentationDependencies = dependencyGroup {
    ui()
    navigation()
    log()
    dagger()
    coroutines()
    debugTools()
    kotlinSdk()
    glide()
    retrofit()
    testing()
}

ext.kotlinSdk = dependencyGroup {
    implementation "androidx.core:core-ktx:${ktxVersion}"
}

ext.lifecycle = dependencyGroup {
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
}

ext.debugTools = dependencyGroup {
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
}

ext.db = dependencyGroup {
    //Room
    api "androidx.room:room-runtime:$roomVersion"
    api "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
}

ext.ui = dependencyGroup {
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.3.1'

    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"

}

ext.glide = dependencyGroup {
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
}

ext.retrofit = dependencyGroup {
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttp3Version}"
    implementation "com.squareup.okhttp3:okhttp:${okhttp3Version}"
    implementation 'com.google.code.gson:gson:2.9.0'
}

ext.coroutines = dependencyGroup {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
}

ext.dagger = dependencyGroup {
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
}

ext.navigation = dependencyGroup {
    implementation 'com.github.terrakok:cicerone:7.1'
}

ext.log = dependencyGroup {
    implementation "com.jakewharton.timber:timber:${timberVersion}"
    implementation 'com.google.firebase:firebase-analytics:17.2.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.1.0'
}
ext.testing = dependencyGroup {
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
}
